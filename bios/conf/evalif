#!/bin/csh -f
# *** evalif is part of bios/roof/ ***
# 2002-03-12  wolfd: add 'make'
# started: 2000-07-19  wolfd/brogerc

set cpp=/usr/lib/cpp
if (! -x $cpp)  set cpp=/usr/bin/cpp
if (! -x $cpp) then
  echo "PROBLEM in evalif.csh :"
  echo "Sorry, cannot find 'cpp'. I am looking at location '$cpp', "
  echo "which seems to be wrong."  
  echo "Please enter the location of cpp on this system"
  echo "into bios/roof/evalif.csh and say 'make'."
  exit 1
endif

if ($#argv < 3) then
  echo ""
  echo "usage: evalif mode symbol infile outfile"
  echo "Process #ifdef/#ifndef directives with"
  echo "  #define 'symbol' 1"
  echo "in effect. Reads from 'infile', writes to 'outfile'."
  echo "Inserts a comment into 'outfile' making it recognizable as generated."
  echo "'outfile' is set to read-only."
  echo "'mode' can be"
  echo "C      -- C-style comment prepended to 'outfile'"
  echo "script -- #-style comment appended to 'outfile' and"
  echo "          outfile set to executable"
  echo "make   -- #-style comment prepended to 'outfile'"
  echo "html   -- HTML-style comment prepended to 'outfile'"
  echo "Input syntax:"
  echo "The input is processed by the C compiler preprocessor,"
  echo "which looks at lines starting with '#'. Therefore, if a line"
  echo "in the 'outfile' should begin with a '#', prefix it with '@'; e.g."
  echo "'#define XXX' must be written as '@#define XXX'"
  echo "version 2004-02-12 ... part of BIOS"
  exit 1
endif

set tmpfile1 = evalif.tmp.1.$$
set tmpfile2 = evalif.tmp.2.$$
set comment = "This file ($4) was generated by program evalif with symbol $2 from source file $3 on `date '+%Y-%m-%d'`. Usually one would not edit this file, but the source file."

if (! -r $3) then
  echo "cannot read input file $3. Abort."
  exit 1
endif

if (-f "$4") /bin/rm -f "$4"

if (-f $tmpfile1) then
  echo "$tmpfile already exists. Abort."
  exit 1
endif

if ($1 == "C") then
  echo "/* $comment */" > $tmpfile1
  cat $3 >> $tmpfile1
else if ($1 == "script") then
  cat $3 > $tmpfile1
  echo "@# /* $comment */" >> $tmpfile1
else if ($1 == "html") then
  echo "<! /* $comment */ >" > $tmpfile1
  cat $3 >> $tmpfile1
else if ($1 == "make") then
  echo "@# /* $comment */" > $tmpfile1
  cat $3 >> $tmpfile1
else 
  echo "unknown option '$1'. Use evalif without arguments for help."
  exit 1
endif

$cpp -P -C -D$2 $tmpfile1 $tmpfile2
perl -ne 's/^@#/#/; if ($_ eq "\n") { if ($l ne "\n") {print};} else {print}; $l=$_' <$tmpfile2 >$4
/bin/rm $tmpfile1 $tmpfile2

chmod ugo-w $4
if ($1 == "script") then
  chmod ugo+x $4
endif

